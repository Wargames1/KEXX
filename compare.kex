******************************************************************************
* xcompare - Perform line by line comparisons on two files in split          *
*            screen mode.  Allowing for editting of any differences found.   *
*                                                                            *
* Kedit the files you wish to compare in split screen mode:                  *
*                    "KEDIT FILE1.TXT"                                       *
*                    "KEDIT FILE2.TXT"                                       *
*                    "SCREEN 2"           ;; Or.. "screen 2 v"               *
*                    "XCOMPARE"           ;; starts compare from top.        *
*                    "XCOMPARE CURLINE"   ;; starts from where you left off  *
*                                                                            *
* Edit two files and enter "XCOMPARE" to send both files to the top and      *
* begin comparing.  If you get to the end of one of the files you'll hear    *
* the familiar "Shave and a hair cut, 2 bits".                               *
* If a difference is found, the comparisons will stop and a beep             *
* will sound.  You can adjust the files so that the files match and enter    *
* "XCOMPARE CURLINE" to begin searching where you left off.                  *
*                                                                            *
*                                                                            *
* For ease of use, place the following in your profile.kex:                  *
* 'DEFINE C-X XCOMPARE'                                                      *
* 'DEFINE A-X XCOMPARE CURLINE'                                              *
* 'DEFINE A-U XCOMPARE UP'                                                   *
*                                                                            *
******************************************************************************
* Legal Stuff:
*
* MIT License
* Copyright (c) 2024 Gordon, M.D. https://gofund.me/a04e971f  
*
* Permission is hereby granted, free of charge, to any person obtaining a copy
* of this software and associated documentation files (the "Software"), to deal
* in the Software without restriction, including without limitation the rights
* to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
* copies of the Software, and to permit persons to whom the Software is
* furnished to do so, subject to the following conditions:

* The above copyright notice, and this permission notice shall be included in all* 
* copies or substantial portions of the Software.

* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
* IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
* FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
* AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
* LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
* OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
* SOFTWARE.
*
c_speed_of_computer = 750 /* Delay factor to hear shave-and-a-haircut at end*/
                          /* 20 for a 386-33, 50 for a 486-50,              */
                          /* 200 for a pentium-75, may need to experiment   */

c_refresh_rate      = 4   /* Number of lines to compare before each refresh.*/
                          /* Refreshing screens on each compare noticably   */
                          /* impacts performance on slow machines. I like 4.*/
r = 0
c_exact = 0 /* 0 - remove multiple spaces from input lines, 1 do not remove multiple spaces from input lines */
'editv get xcompare'
if xcompare = '' then do; 'define xcompare.kex'; 'editv set xcompare 1'; end;
if word(scre.1(),3) = '' then
do
   'emsg Need two screens active.'
   exit
end
direction = "+1"
if arg(1) = '' then
do
   'top'
   'sos tabcmdf'
   'top'
end
else
do
   arg args
   if args = "UP" then direction = "-1";
end
'preserve'
'set wrap off'
'sos tabcmdf'
'preserve'
'set wrap off'
zone1 = zone.1()
zone2 = zone.2()
/* Jump to command line of second file, (makes curline the focus line ) */
'sos tabcmdf'
do until lrc <> 0
   'locate' direction
   lrc = rc
   if lrc <> 0 then
   do
/*     See if second file is at eof also. If so then files are equal. */
      'refresh'
      'sos tabcmdf'
      'locate' direction
      if rc <> 0 then leave
      'restore'
      'refresh'
      'sos alarm'
      'sos tabcmdf'
      'restore'
      if direction = "+1" then
         'emsg' 'Premature end of file.'
      else
         'emsg' 'Premature start of file.'
      exit
   end
   str1 = remove_spaces(substr(curline.3(),zone1, (zone2 - zone1 + 1)))
   'sos tabcmdf'
   'locate' direction
   saverc = rc
   r = r + 1
   if r = c_refresh_rate then
   do
      'refresh'
      r = 0
   end
   if saverc <> 0 then
   do
/*     See if second file is at eof also. If so then files are equal.     */
      'restore'
      'sos tabcmdf'
      'restore'
      'locate' direction
      if rc <> 0 then leave
      if direction = "+1" then
      do
         'emsg' 'Premature end of file.'
         'sos alarm'
         'locate -1'
      end
      else
      do
         'emsg' 'Premature start of file.'
         'sos alarm'
         'locate +1'
      end
      'refresh'
      exit
   end

   str2 = remove_spaces(substr(curline.3(),zone1, (zone2 - zone1 + 1)))
   if str1 <> str2 then
   do
      'refresh'
      'restore'
      'sos tabcmdf'
      ls1 = length(str1)
      do col = 1 to ls1
         if substr(str1, col, 1) <> substr(str2,col,1) then
            leave
      end
      col = col + zone1 - 1
      'emsg' 'Difference detected in column='col'.  "'substr(str1, col, 1)'" <> "'substr(str2, col, 1)'"'
      'restore'
     /* Following 2 lines added for Kedit For Windows since curline is not highlighted. */
      'tag :'line.1()
      'sos alarm'
      'sos tabcmdf'
     /* Following 2 lines added for Kedit For Windows since curline is not highlighted. */
      'tag :'line.1()
      exit
   end
end
/* Files are equal play "shave and a haircut, 2 bits." */
msg 'End of File encountered, No differences.'
sos alarm


two   = 2 * c_speed_of_computer
three = 3 * c_speed_of_computer
four  = 4 * c_speed_of_computer
six   = 6 * c_speed_of_computer
eight = 8 * c_speed_of_computer

do i = 1 to six;   end; sos alarm;
do i = 1 to two;   end; sos alarm;
do i = 1 to two;   end; sos alarm;
do i = 1 to four;  end; sos alarm;
do i = 1 to eight; end; sos alarm
do i = 1 to three; end; sos alarm;
exit

remove_spaces: procedure expose c_exact
/* get rid of multiple spaces */
parse arg string
if c_exact = 1 then return string  /* don't do it if exact comparison flag on */
s = ""
do i = 1 to words(string)
   s = s strip(subword(string,i,1))
end
return s
